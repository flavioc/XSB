/* File:      gensym.P
** Author(s): Kostis F, Sagonas, Jiyang Xu 
** Contact:   xsb-contact@cs.sunysb.edu
** 
** Copyright (C) The Research Foundation of SUNY, 1986, 1993-1999
** Copyright (C) ECRC, Germany, 1990
** 
** XSB is free software; you can redistribute it and/or modify it under the
** terms of the GNU Library General Public License as published by the Free
** Software Foundation; either version 2 of the License, or (at your option)
** any later version.
** 
** XSB is distributed in the hope that it will be useful, but WITHOUT ANY
** WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
** FOR A PARTICULAR PURPOSE.  See the GNU Library General Public License for
** more details.
** 
** You should have received a copy of the GNU Library General Public License
** along with XSB; if not, write to the Free Software Foundation,
** Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
**
** $Id$
** 
*/


/*======================================================================*/
/* prepare for compiler and type inference				*/
/*======================================================================*/

prepare :- conset(gennum_val, 300).	/* for gennum and gensym */
		/* big number to avoid conflict in internal variable */
		/* generated by gennum with read-in variable numbered */
		/* from 0 above */

/*----conlength---------------------------------------------------------*/

conlength(Con, Length) :-
	conname(Con, Name), str_len(Name, Len), Length = Len.

/*----gennum------------------------------------------------------------*/

gennum(N) :- conget(gennum_val, O), N is O + 1, conset(gennum_val, N).

/*----gensym------------------------------------------------------------*/

gensym(R, C) :-
	gennum(N), atom_codes(R, N1), name(N, N2),
	append(N1, N2, N3), atom_codes(C, N3).

/*----copyterm----------------------------------------------------------*/
/* Get a copy of the Term, with a new set of variables.			*/

copyterm(Term, NewTerm) :- copyterm(Term, NewTerm, _).

copyterm(Term, NewTerm, VarDict) :- var(Term), !,
	dict_lookup(Term, NewTerm, VarDict).
copyterm(Term, Term, _) :- atomic(Term), !.
copyterm(Term, NewTerm, VarDict) :- 
	functor(Term, Func, Arity),
	functor(NewTerm, Func, Arity),
	copyterm_args(Term, NewTerm, Arity, VarDict).

copyterm_args(_, _, 0, _) :- !.
copyterm_args(Term, NewTerm, N, VarDict) :-
	arg(N, Term, Arg),
	copyterm(Arg, NewArg, VarDict),
	arg(N, NewTerm, NewArg),
	N1 is N - 1,
	copyterm_args(Term, NewTerm, N1, VarDict).

conset(Con, Val) :- 
	conpsc(Con, Psc), psc_set_prop(Psc, Val).

conget(Con, Val) :- 
	conpsc(Con, Psc), psc_prop(Psc, Val0), Val = Val0.

coninc(Con) :-
	conpsc(Con, Psc), psc_prop(Psc, N),
	NewN is N+1, psc_set_prop(Psc, NewN).

/*======================================================================*/
/* counter routines. A counter is a unary term.				*/
/*======================================================================*/

newcounter(C) :- conpsc(C, Psc), psc_set_type(Psc, 8),
	arg(1, C, V), 
	( integer(V), V0 = V
	; V0 = 0
	),
	psc_set_prop(Psc, V0).

inccounter(C) :- conpsc(C, Psc), psc_prop(Psc, V), 
		 V1 is V + 1, psc_set_prop(Psc, V1).

getcounter(C) :- conpsc(C, Psc), psc_prop(Psc, V), arg(1, C, V).

