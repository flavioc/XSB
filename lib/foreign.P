/* File:      foreign.P
** Author(s): Jiyang Xu, Kostis Sagonas
** Contact:   xsb-contact@cs.sunysb.edu
** 
** Copyright (C) The Research Foundation of SUNY, 1986, 1993-1998
** Copyright (C) ECRC, Germany, 1990
** 
** XSB is free software; you can redistribute it and/or modify it under the
** terms of the GNU Library General Public License as published by the Free
** Software Foundation; either version 2 of the License, or (at your option)
** any later version.
** 
** XSB is distributed in the hope that it will be useful, but WITHOUT ANY
** WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
** FOR A PARTICULAR PURPOSE.  See the GNU Library General Public License for
** more details.
** 
** You should have received a copy of the GNU Library General Public License
** along with XSB; if not, write to the Free Software Foundation,
** Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
**
** $Id$
** 
*/

:- compiler_options([xpp_on]).

#include "flag_defs.h"

compile_cH(Modname, Base, Options, ExList, Dir) :-
	/* ldoption is needed at compile time for ELF systems,
	   so compile_H is now done first. */
	compile_H(Modname,Base,Options,ExList,LdOption,CompComm,TempFile),
	compile_c(Modname,Base,Options,Dir,LdOption,CompComm,TempFile).


compile_c(Modname, Base, Options, Dir, LdOption, CompComm, TempFile) :-
	xsb_configuration(configuration, Configuration),
	cc_name(Options, CC_Name), cc_options(Options, CC_Opts),
	message(('[Compiling C file ', Base, '.c using ', CC_Name, ']')),
	( var(LdOption) -> LdOption = '' ; true ),
	( var(TempFile) -> TempFile = ''; true),
	(   var(CompComm)
	->  (	str_sub('solaris', Configuration)
	    ->	shell([CC_Name, ' -s -o ', Base, '.so', ' -G ',
		       Base, '.c ', TempFile, ' ', CC_Opts, ' ', LdOption])
	    ;	str_sub('linux', Configuration)
	    ->	shell([CC_Name, ' -s -o ', Base, '.so', ' -shared ',
		       Base, '.c ', TempFile, ' ', CC_Opts, ' ', LdOption])
	    /*
	    , message((CC_Name, ' -s -o ', Base, '.so', ' -shared ',
		       Base, '.c ', TempFile, ' ', CC_Opts, ' ', LdOption))
	    */
	    ;	str_sub('alpha', Configuration)
	    ->	shell([CC_Name, ' -c ', Base, '.c ', TempFile, ' ',CC_Opts]),
		shell(['ld -expect_unresolved \* -O1 -shared -o ',
		       Base, '.so ', Base, '.o ', LdOption])
	    ;	str_sub('windows', Configuration)
	    ->	warning(('You must create the ', Modname,'.dll'))
	    ;	shell([CC_Name, ' -c ', Base, '.c ', TempFile, ' ', CC_Opts]),
	        /* The following is here because C compilers usually put 
	           the object file in the current directory
	           and not in the source one. */
	        (   Dir == './'
		->  true
		;   shell(['mv ', Modname, '.o ', Dir])
		)
	    )
        ;   shell([CompComm])
        ).

cc_name(Options, CC_Name) :-
	( memberchk(cc(CC_Name), Options), atom(CC_Name) -> true
	;   xsb_configuration(compiler, CC_Name)
	).
cc_options(Options, CC_Opts) :-
	( memberchk(cc_opts(CC_Opts), Options) -> true ; CC_Opts = '' ).

compile_H(Modname, Base, Options, ExList, LdOption, CompComm, TempFile) :-
	hdr_ext(HExt),
	str_cat(Base, HExt, HFile),
	( file_exists(HFile) ->
	    (	Options == [] 
	    ->	M = ''
	    ;	M = ' (Prolog compiler options are ignored)'
	    ),
	    message(('[Compiling Foreign Module ', Base, M, ']')),
	    cputime(TimeStart),
	    seeing(OldFile),
	    telling(OldTell),
%%%	    see(HFile),
	    repeat,
	    open_or_xpp_file(HFile,CPPprocess),
	    read_clauses(Modname,ExList,'',LdOption,CompComm,TempFile), !,
	    (	CPPprocess = none, ! 
	    ;	process_control(CPPprocess,wait),
		stat_flag(CURRENT_INPUT,FromPort),
		file_close(FromPort)
	    ),
	    closefile(TempFile),
	    see(OldFile),
	    tell(OldTell),
	    conset(xpp_on,0),
%%%         seen,
	    length(ExList, Count),
	    obj_ext(OExt),
	    str_cat(Base, OExt, OFile),
	    tell(OFile),
	    co_asmhead(Modname, LdOption, Count),
	    co_asmsym(ExList),
	    told,
	    time_message(TimeStart, (Modname, ' compiled'))
	;   abort(('File ', HFile, ' for foreign module does not exist'))
	),
	!.

read_clauses(ModName, ExList, LdOptionIn, LdOptionOut, CompComm, TempFile) :-
	read(Clause),
	(   Clause == end_of_file
	->  LdOptionOut = LdOptionIn
        ;   (   Clause = (':-'(export(Exports)))
	    ->	add_to_exlist(Exports, ExList),
		LdOptionMid = LdOptionIn
	    ;	Clause = (':-'(ldoption(Opt)))
	    ->	form_ld_option(Opt, LdOptionIn, LdOptionMid)
	    ;	Clause = (':-'(compiler_options(CompOpt)))
	    ->	(memberchk(xpp_on,CompOpt), conget(xpp_on,0)
		->  closefile(TempFile),
		    conset(xpp_on,1),
		    seen, 
		    fail
		;   LdOptionMid = LdOptionIn)
	    ;	Clause = (':-'(compile_command(CompComm)))
	    ->	LdOptionMid = LdOptionIn
	    ;	Clause = (':-'(foreign_pred(PredDecl)))
	    ->	checkfile(ModName, TempFile), % creates tempfile if necessary
	        process_from(PredDecl,Exports),
		add_to_exlist(Exports,ExList),
		LdOptionMid = LdOptionIn
	    ;	LdOptionMid = LdOptionIn % all other clauses are ignored
	    ),
            read_clauses(ModName, ExList, LdOptionMid, LdOptionOut, 
			 CompComm, TempFile)
        ).


form_ld_option([], In, In) :- !.
form_ld_option([Opt|Opts], In , Out) :- !,
	form_ld_option(In, Opt, Mid), form_ld_option(Opts, Mid, Out).
form_ld_option(Opt, In, Out) :-
	str_cat(In, Opt, M), str_cat(M, ' ', Out).

add_to_exlist(P/A, ExList) :- !, attach(P/A, ExList).
add_to_exlist((Pred, Preds), ListType) :- !,
        add_to_exlist(Pred, ListType),
        add_to_exlist(Preds, ListType).

co_asmhead(Modname, LdOption, PSC_Count) :-
	asm_magic(8),
	str_len(Modname, Length),
	put(Length),
	write(Modname),
	( LdOption == '' -> put(0) % No ldoption was specified (length is 0).
	; str_len(LdOption, LdoLen), put(LdoLen), write(LdOption)
	),
	write_int(PSC_Count).

co_asmsym([]).
co_asmsym([One|ExList]) :- co_one(One), co_asmsym(ExList).

co_one(P/A) :-
	str_len(P, L),
	put(0),		% domain = ex(ported)
	put(13),	% type = T_FORN (in psc.h)
	put(A),		% arity = a
	put(L),		% length
	write(P).		

/*======================================================================*/
/* format: 
		+-----------------------+
		| magic = 8		|
		+-----------------------+------------------+
		| len  |  module name (len size)	   |
		+-----------------------+----------+-------+
		| len  |  ld option		   |
		+-----------------------+----------+
		| psc count		|
		+-----------------------+

		+-----------------------+-------------------------------+
	count:	| 0 | 13 | 0 |arity|len |  symbol name			|
		+-----------------------+-------------------------------+
		... ... ... ...
		... ... ... ...

	Note that a foreign module is always a Module (len size =/= 0).
*/
/*======================================================================*/

%% Wrapper-generation
%% Author: Luis Castro
%% Date: 08/99

checkfile(ModName, TempFile) :- 
	var(TempFile), !,
	str_cat('xsb_wrap_',ModName,TTName),
	str_cat(TTName,'.c',TempFile),
	tell(TempFile),
	write_header.
checkfile(_ModName,_TempFile).

closefile(TempFile) :- 
	var(TempFile), !.
closefile(_TempFile) :- 
	told.



