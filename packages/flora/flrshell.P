/* File:      flrshell.P
**
** Author(s): Guizhen Yang       (Flora)
**    	      Bertram Ludaescher (Flip, the Flora precursor)
** Contact:   xsb-contact@cs.sunysb.edu
**
** Copyright (C) Bertram Ludaescher, 1998
** Copyright (C) The Research Foundation of SUNY, 1999
**
** XSB is free software; you can redistribute it and/or modify it under the
** terms of the GNU Library General Public License as published by the Free
** Software Foundation; either version 2 of the License, or (at your option)
** any later version.
** 
** XSB is distributed in the hope that it will be useful, but WITHOUT ANY
** WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
** FOR A PARTICULAR PURPOSE.  See the GNU Library General Public License for
** more details.
** 
** You should have received a copy of the GNU Library General Public License
** along with XSB; if not, write to the Free Software Foundation,
** Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
**
** $Id$
**
*/


:- import
	write/1, writeln/1, repeat/0, atom_chars/2, abort/0,
	cputime/1, push_abort_cutpoint/0, pop_abort_cutpoint/0,
	file_exists/1
   from standard.

:- import member/2 from basics.

:- import trie_assert/1, trie_retract_all/1 from tables.

:- import slash/1 from machine.

:- import fmt_write_string/3 from file_io.

:- import current_predicate/1 from curr_sym.

:- import consult/2, compile/1, compile/2 from consult.

:- import
	bootstrap_package/2, package_configuration/2, unload_package/1
   from packaging.

:- import
	'_$_$_flora_read_parse_compile'/6,
	'_$_$_flora_flcompile'/5,
	'_$_$_flora_loadlist'/1,
	'_$_$_flora_dynloadlist'/1,
	'_$_$_flora_check_filename'/1,
	'_$_$_write_flora_warnheader'/0,
	'_$_$_flora_discard_tokens'/1,
	flcompile/1, flcompile/2, maxerr/1, flconsult/1, flconsult/2,
	load/1, dynload/1, dyncompile/1, dyncompile/2, dynconsult/1,
	dynconsult/2, displayln_warning/1
   from flrutils.

:- import glob_directory/4 from wildmatch.

:- dynamic '_$_$_flora_switch'/1.

/*********************************************************************/
'_$_$_flora_shell' :-
	( package_configuration(dir(flora),FloraDir) ->
	    true

	  ;
	    bootstrap_package(flora,flora),
	    package_configuration(dir(flora),FloraDir)
	),
	slash(S),
	fmt_write_string(Lib,'%s%slib%sflrdisplay',f(FloraDir,S,S)),
	[tables, Lib, flrutils],
	'_$_$_flora_welcome_msg',
	all,
	push_abort_cutpoint,
	'_$_$_flora_shell_loop'.


/*********************************************************************/
'_$_$_flora_welcome_msg' :-
	nl,
	write('FLORA Version '),
	package_configuration(version(flora),V),
	write(V), writeln(' loaded.'), nl,
	writeln('Type `help.'' to display the help message'),
	nl,
	writeln('Type `rundemo(demoName).'''),
	write('     to run the demos in '),
	slash(S),
	package_configuration(dir(flora),FloraDir),
	fmt_write("%s%sdemos\n\n",f(FloraDir,S)),
	nl,
	!.


/*********************************************************************/
'_$_$_flora_shell_loop' :-
	repeat,
	pop_abort_cutpoint,
	push_abort_cutpoint,
	seen,
	see(userin),
	told,
	tell(userout),
	nl, write('FLORA> ?- '),
	'_$_$_flora_read_parse_compile'(static_mode,
					[atom('$?'),atom('$-')],
					Tokens,
					Rules,
					Status,
					Opts),
	( member(noop,Status) ->
	    nl, S=Status

	  ; member(error(_),Status) ->
	    ( (member(eof,Status); member(atom(rule_delimeter),Tokens)) ->
		S=Status
	      ;
	        '_$_$_flora_discard_tokens'(S)
	    )

	  ;
	    S=Status,
	    (Opts=[] -> true; '_$_$_flora_check_library'(Opts)),
	    Rules=[query(Gs,NVs)],
	    '_$_$_flora_print_answers'(Gs,NVs)
	),
	(member(eof,S) -> end ; fail).


/*********************************************************************/
'_$_$_flora_check_library'(Opts) :-
	package_configuration(dir(flora),FloraDir),
	slash(S),
	( member(flrassert,Opts) ->
	    ( current_predicate('_$_$_flora_assert'/1) ->
	       true
	      ;
	       fmt_write_string(Assert,'%s%slib%sflrassert.P',f(FloraDir,S,S)),
	       consult(Assert)
	    )
	  ;
	    true
	),
	( member(flraggregate,Opts) ->
	    ( current_predicate('_$_$_flora_min'/3) ->
	      true
	     ;
	      fmt_write_string(Aggr,'%s%slib%sflraggregate.P',f(FloraDir,S,S)),
	      consult(Aggr)
	    )
	  ;
	    true
	),
	( member(skolem,Opts) ->
	    nl,
	    displayln_warning('skolemization may require equality checking')
	  ;
	   true
	).


/*********************************************************************/
'_$_$_flora_print_answers'(Gs,NVs) :-
	( '_$_$_flora_switch'(all) ->
	    '_$_$_flora_print_all'(Gs,NVs)
	  ;
	    '_$_$_flora_print_one'(Gs,NVs)
	).


/*********************************************************************/
help :-
	nl,
	writeln('FLORA commands:'), nl,
	writeln('  help                         : show this info'),
	writeln('  compile(FILE[.P])            : compile FILE.P; create FILE.O'),
	writeln('  flcompile(FILE.[flr])        : compile FILE.flr; create FILE.P and FILE.O'),
	writeln('  flcompile(FILE.[flr],[...])  : flcompile(FILE) with options [...]'),
	writeln('  flconsult(FILE.[flr])        : compile FILE.flr; consult FILE.P'),
	writeln('  flconsult(FILE.[flr],[...])  : flconsult(FILE) with options [...]'),
	writeln('  load(FILE[.{P|O|flr}])       : consult FILE.flr, FILE.P or FILE.O'),
	writeln('  [FILE[.{P|O|flr}],...]       : consult a list of .flr, .P, or .O files'),
	writeln('  dyncompile(FILE[.flr])       : compile FILE.flr to dynamic code'),
	writeln('  dyncompile(FILE[.flr],[...]) : dyncompile(FILE) with options [...]'),
	writeln('  dynconsult(FILE[.flr])       : dyncompile FILE.flr; dynamically load FILE.P'),
	writeln('  dynconsult(FILE[.flr],[...]) : dynconsult(FILE) with options [...]'),
	writeln('  dynload(FILE[.flr])          : dynamically load FILE.flr or FILE.P'),
	writeln('  {FILE[.EXT],...}             : dynload a list of .flr or .P files'),
	writeln('  rundemo(FILE[.flr])          : flconsult a demo from FLORA demos directory'),
	writeln('  rundemo(FILE[.flr],[...])    : rundemo(FILE) with options [...]'),
	writeln('  abolish_all_tables           : flush all tabled data'),
	writeln('  all                          : show all solutions (default)'),
	writeln('  one                          : show solutions one by one'),
	writeln('  maxerr(all/N)                : set/show the max number of errors FLORA reports'),
	writeln('  end                          : say CIAO to FLORA'),
	writeln('  halt                         : quit FLORA and XSB').


/*********************************************************************/
rundemo(X,Dtvs) :-
	'_$_$_flora_check_filename'(X),
	package_configuration(dir(flora),FloraDir),
	slash(Slash),
	%% Add the demo directory to library_directory
	fmt_write_string(DemoDir, '%s%sdemos', f(FloraDir, Slash)),
	(library_directory(DemoDir)
	       -> true
		; assert(library_directory(DemoDir))
	),
	flconsult(X,Dtvs),
	retract(library_directory(DemoDir)).

rundemo(X) :-
	'_$_$_flora_check_filename'(X),
	package_configuration(dir(flora),FloraDir),
	slash(Slash),
	%% Add the demo directory to library_directory
	fmt_write_string(DemoDir, '%s%sdemos', f(FloraDir, Slash)),
	(library_directory(DemoDir)
	       -> true
		; assert(library_directory(DemoDir))
	),
	flconsult(X),
	retract(library_directory(DemoDir)).


/*********************************************************************/
all :-
	( '_$_$_flora_switch'(all) ->
	    true
	  ;
	    trie_assert('_$_$_flora_switch'(all))
	).


/*********************************************************************/
one :-
	trie_retract_all('_$_$_flora_switch'(all)).


/*********************************************************************/
end :-
	unload_package(flora),
	nl, writeln('CIAO !'), nl,
	pop_abort_cutpoint,
	abort.


/*********************************************************************/
%% Take all *,flr files in ./flora/demos/ and flora-compile them.
%% This relies on the availability of Posix wildcard matcher.
%% It is used only for configuring the demos.
flcompile_all_demos  :-
	package_configuration(dir(flora),FloraDir),
	slash(S),
	fmt_write_string(DemoDir,'%s%sdemos',f(FloraDir,S)),
        glob_directory('*.flr', DemoDir, _, DemoList),
	(library_directory(DemoDir) -> true
	    ;
	    assert(library_directory(DemoDir))
	),
	flcompile_demo(DemoList),
	retract(library_directory(DemoDir)).

flcompile_demo([D|Rest]) :- 
	'_$_$_flora_flcompile'(static_mode,[],D,noforce,_),
	flcompile_demo(Rest).
flcompile_demo([]).



/********************************************************************/
:- table '_$_$_flora_fd_dyn'/3.
:- table '_$_$_flora_mvd_dyn'/3.
:- table '_$_$_flora_ifd_dyn'/3.
:- table '_$_$_flora_imvd_dyn'/3.
:- table '_$_$_flora_isa_dyn'/2.
:- table '_$_$_flora_sub_dyn'/2.
:- table '_$_$_flora_fs_dyn'/3.
:- table '_$_$_flora_mvs_dyn'/3.
:- table '_$_$_flora_mvd_dyn'/2.
:- table '_$_$_flora_imvd_dyn'/2.
:- table '_$_$_flora_exists_dyn'/1.
:- table '_$_$_flora_fd_rhs_dyn'/3.
:- table '_$_$_flora_mvd_rhs_dyn'/3.
:- table '_$_$_flora_ifd_rhs_dyn'/3.
:- table '_$_$_flora_imvd_rhs_dyn'/3.
:- table '_$_$_flora_isa_rhs_dyn'/2.
:- table '_$_$_flora_sub_rhs_dyn'/2.
:- table '_$_$_flora_fs_rhs_dyn'/3.
:- table '_$_$_flora_mvs_rhs_dyn'/3.
:- table '_$_$_flora_mvd_rhs_dyn'/2.
:- table '_$_$_flora_imvd_rhs_dyn'/2.


:- dynamic '_$_$_flora_fd_dyn'/3.
:- dynamic '_$_$_flora_mvd_dyn'/3.
:- dynamic '_$_$_flora_ifd_dyn'/3.
:- dynamic '_$_$_flora_imvd_dyn'/3.
:- dynamic '_$_$_flora_isa_dyn'/2.
:- dynamic '_$_$_flora_sub_dyn'/2.
:- dynamic '_$_$_flora_fs_dyn'/3.
:- dynamic '_$_$_flora_mvs_dyn'/3.
:- dynamic '_$_$_flora_mvd_dyn'/2.
:- dynamic '_$_$_flora_imvd_dyn'/2.
:- dynamic '_$_$_flora_exists_dyn'/1.
:- dynamic '_$_$_flora_fd_rhs_dyn'/3.
:- dynamic '_$_$_flora_mvd_rhs_dyn'/3.
:- dynamic '_$_$_flora_ifd_rhs_dyn'/3.
:- dynamic '_$_$_flora_imvd_rhs_dyn'/3.
:- dynamic '_$_$_flora_isa_rhs_dyn'/2.
:- dynamic '_$_$_flora_sub_rhs_dyn'/2.
:- dynamic '_$_$_flora_fs_rhs_dyn'/3.
:- dynamic '_$_$_flora_mvs_rhs_dyn'/3.
:- dynamic '_$_$_flora_mvd_rhs_dyn'/2.
:- dynamic '_$_$_flora_imvd_rhs_dyn'/2.


'_$_$_flora_fd'(O,M,R)       :- '_$_$_flora_fd_dyn'(O,M,R).
'_$_$_flora_mvd'(O,M,R)      :- '_$_$_flora_mvd_dyn'(O,M,R).
'_$_$_flora_ifd'(O,M,R)      :- '_$_$_flora_ifd_dyn'(O,M,R).
'_$_$_flora_imvd'(O,M,R)     :- '_$_$_flora_imvd_dyn'(O,M,R).
'_$_$_flora_isa'(O1,O2)      :- '_$_$_flora_isa_dyn'(O1,O2).
'_$_$_flora_sub'(O1,O2)      :- '_$_$_flora_sub_dyn'(O1,O2).
'_$_$_flora_fs'(O,M,R)       :- '_$_$_flora_fs_dyn'(O,M,R).
'_$_$_flora_mvs'(O,M,R)      :- '_$_$_flora_mvs_dyn'(O,M,R).
'_$_$_flora_mvd'(O1,O2)      :- '_$_$_flora_mvd_dyn'(O1,O2).
'_$_$_flora_imvd'(O1,O2)     :- '_$_$_flora_imvd_dyn'(O1,O2).
'_$_$_flora_exists'(O)       :- '_$_$_flora_exists_dyn'(O).


/*****************************************************************************
  closure rules for X::Y
*****************************************************************************/
:- table '_$_$_flora_subclass'/2.

'_$_$_flora_subclass'(X,X) :- '_$_$_flora_sub'(X,_).
'_$_$_flora_subclass'(X,X) :- '_$_$_flora_sub'(_,X).
'_$_$_flora_subclass'(X,Y) :- '_$_$_flora_sub'(X,Y).

'_$_$_flora_subclass'(X,Y) :-
	'_$_$_flora_subclass'(X,Z), 
	'_$_$_flora_subclass'(Z,Y),
	(X \= Z -> true ; Z\=Y),
	( X = Y ->
	    '_$_$_write_flora_warnheader',
	    write('cyclic subclass hierarchy: '),
	    write(X), write('::'), write(Z), write('::'), write(Y), nl
	  ;
	    true
	).


/*****************************************************************************
  closure rules for X:Y, X::Z implies X:Z
*****************************************************************************/
:- table '_$_$_flora_isa'/2.

'_$_$_flora_isa'(O,C) :-
	'_$_$_flora_subclass'(C1,C),
	'_$_$_flora_isa'(O,C1).


/*****************************************************************************
  rules for monotonic inheritance of behavior
*****************************************************************************/
:- table '_$_$_flora_fd'/3.
:- table '_$_$_flora_mvd'/3.
:- table '_$_$_flora_ifd'/3.
:- table '_$_$_flora_imvd'/3.

'_$_$_flora_fd'(O,MethodArgs,R) :-
	'_$_$_flora_isa'(O,Class),
	'_$_$_flora_ifd'(Class,MethodArgs,R).

'_$_$_flora_mvd'(O,MethodArgs,R) :-
	'_$_$_flora_isa'(O,Class),
	'_$_$_flora_imvd'(Class,MethodArgs,R).

'_$_$_flora_ifd'(Sub,MethodArgs,R) :-
	'_$_$_flora_subclass'(Sub,Class),
	'_$_$_flora_ifd'(Class,MethodArgs,R).

'_$_$_flora_imvd'(Sub,MethodArgs,R) :-
	'_$_$_flora_subclass'(Sub,Class),
	'_$_$_flora_imvd'(Class,MethodArgs,R).


/*****************************************************************************
  rules for nonmonotonic inheritance of behavior

:- table '_$_$_flora_fd'/3.
:- table '_$_$_flora_mvd'/3.
:- table '_$_$_flora_ifd'/3.
:- table '_$_$_flora_imvd'/3.
:- table '_$_$_flora_defined_fd'/2.
:- table '_$_$_flora_defined_mvd'/2.
:- table '_$_$_flora_defined_ifd'/2.
:- table '_$_$_flora_defined_imvd'/3.
:- table '_$_$_flora_overwritten_fd'/3.
:- table '_$_$_flora_overwritten_mvd'/3.
:- table '_$_$_flora_overwritten_ifd'/3.
:- table '_$_$_flora_overwritten_imvd'/3.

'_$_$_flora_fd'(Object,Method,Value) :-
	tnot('_$_$_flora_defined_fd'(Object,Method)),
	'_$_$_flora_isa'(Object,Class),
	'_$_$_flora_ifd'(Class,Method,Value),
	tnot('_$_$_flora_overwritten_fd'(Object,Class,Method)).

'_$_$_flora_defined_fd'(Object,Method) :- '_$_$_flora_fd'(Object,Method,_).

'_$_$_flora_overwritten_fd'(Object,Class,Method) :-
	'_$_$_flora_isa'(Object,SubClass),
	'_$_$_flora_subclass'(SubClass,Class),
	SubClass \= Class,
	'_$_$_flora_ifd'(SubClass,Method,_).


'_$_$_flora_mvd'(Object,Method,Value) :-
	tnot('_$_$_flora_defined_mvd'(Object,Method)),
	'_$_$_flora_isa'(Object,Class),
	'_$_$_flora_imvd'(Class,Method,Value),
	tnot('_$_$_flora_overwritten_mvd'(Object,Class,Method)).

'_$_$_flora_defined_mvd'(Object,Method) :- '_$_$_flora_mvd'(Object,Method,_).

'_$_$_flora_overwritten_mvd'(Object,Class,Method) :-
	'_$_$_flora_isa'(Object,SubClass),
	'_$_$_flora_subclass'(SubClass,Class),
	SubClass \= Class,
	'_$_$_flora_imvd'(SubClass,Method,_).


'_$_$_flora_ifd'(Class,Method,Value) :-
	tnot('_$_$_flora_defined_ifd'(Class,Method)),
	'_$_$_flora_subclass'(Class,Super),
	'_$_$_flora_ifd'(Super,Method,Value),
	tnot('_$_$_flora_overwritten_ifd'(Class,Super,Method)).

'_$_$_flora_defined_ifd'(Class,Method) :- '_$_$_flora_ifd'(Class,Method,_).

'_$_$_flora_overwritten_ifd'(Class,Super,Method) :-
	'_$_$_flora_subclass'(Class,S),
	'_$_$_flora_subclass'(S,Super),
	Super \= S,
	'_$_$_flora_ifd'(S,Method,_).


'_$_$_flora_imvd'(Class,Method,Value) :-
	tnot('_$_$_flora_defined_imvd'(Class,Method)),
	'_$_$_flora_subclass'(Class,Super),
	'_$_$_flora_imvd'(Super,Method,Value),
	tnot('_$_$_flora_overwritten_imvd'(Class,Super,Method)).

'_$_$_flora_defined_imvd'(Class,Method) :- '_$_$_flora_imvd'(Class,Method,_).

'_$_$_flora_overwritten_imvd'(Class,Super,Method) :-
	'_$_$_flora_subclass'(Class,S),
	'_$_$_flora_subclass'(S,Super),
	Super \= S,
	'_$_$_flora_imvd'(S,Method,_).
*****************************************************************************/


/*****************************************************************************
  rules for object existence and empty result sets
*****************************************************************************/
:- table '_$_$_flora_exists'/1.
:- table '_$_$_flora_mvd'/2.
:- table '_$_$_flora_imvd'/2.

'_$_$_flora_exists'(X) :- '_$_$_flora_fd'(X,_,_).
'_$_$_flora_exists'(X) :- '_$_$_flora_fd'(_,_,X).
'_$_$_flora_exists'(X) :- '_$_$_flora_mvd'(X,_,_).
'_$_$_flora_exists'(X) :- '_$_$_flora_mvd'(_,_,X).
'_$_$_flora_exists'(X) :- '_$_$_flora_ifd'(X,_,_).
'_$_$_flora_exists'(X) :- '_$_$_flora_ifd'(_,_,X).
'_$_$_flora_exists'(X) :- '_$_$_flora_imvd'(X,_,_).
'_$_$_flora_exists'(X) :- '_$_$_flora_imvd'(_,_,X).
'_$_$_flora_exists'(X) :- '_$_$_flora_isa'(X,_).
'_$_$_flora_exists'(X) :- '_$_$_flora_isa'(_,X).
'_$_$_flora_exists'(X) :- '_$_$_flora_sub'(X,_).
'_$_$_flora_exists'(X) :- '_$_$_flora_sub'(_,X).

'_$_$_flora_exists'(X) :-
	'_$_$_flora_fd'(_,M,_),
	M =.. [_|L],
	member(X,L).

'_$_$_flora_exists'(X) :-
	'_$_$_flora_mvd'(_,M,_),
	M =.. [_|L],
	member(X,L).

'_$_$_flora_exists'(X) :-
	'_$_$_flora_ifd'(_,M,_),
	M =.. [_|L],
	member(X,L).

'_$_$_flora_exists'(X) :-
	'_$_$_flora_imvd'(_,M,_),
	M =.. [_|L],
	member(X,L).


'_$_$_flora_mvd'(O,M) :- '_$_$_flora_mvd'(O,M,_).

'_$_$_flora_imvd'(O,M) :- '_$_$_flora_imvd'(O,M,_).


/*****************************************************************************
  rules for flattened path expressions on RHS
*****************************************************************************/
'_$_$_flora_fd_rhs'(O,M,R)   :- '_$_$_flora_fd'(O,M,R).
'_$_$_flora_mvd_rhs'(O,M,R)  :- '_$_$_flora_mvd'(O,M,R).
'_$_$_flora_ifd_rhs'(O,M,R)  :- '_$_$_flora_ifd'(O,M,R).
'_$_$_flora_imvd_rhs'(O,M,R) :- '_$_$_flora_imvd'(O,M,R).
'_$_$_flora_mvd_rhs'(O,M)    :- '_$_$_flora_mvd'(O,M).
'_$_$_flora_imvd_rhs'(O,M)   :- '_$_$_flora_imvd'(O,M).
'_$_$_flora_isa_rhs'(O1,O2)  :- '_$_$_flora_isa'(O1,O2).
'_$_$_flora_sub_rhs'(O1,O2)  :- '_$_$_flora_subclass'(O1,O2).
'_$_$_flora_fs_rhs'(O,M,R)   :- '_$_$_flora_fs'(O,M,R).
'_$_$_flora_mvs_rhs'(O,M,R)  :- '_$_$_flora_mvs'(O,M,R).


/********************************************************************/
